import org.apache.tools.ant.filters.*

apply plugin: 'groovy'
apply plugin: 'application'

mainClassName = "com.junbo.apphost.core.JunboApplication"
applicationDefaultJvmArgs = ["-Xmx2048m", "-XX:MaxPermSize=512m", "-Dspring.profiles.active=utility", "-Dfile.encoding=UTF-8",
    "-Xdebug", "-Xrunjdwp:server=y,transport=dt_socket,address=9401,suspend=n",
    "-Dcom.sun.management.jmxremote.port=9070", "-Dcom.sun.management.jmxremote.ssl=false", "-Dcom.sun.management.jmxremote.authenticate=false",
    "-Djava.net.preferIPv4Stack=true"]

dependencies {
    // add sub apps, so we can bundle everything in one zip
    compile junboProject(':apphost-rest')
    compile junboProject(':apphost-crypto')
    compile junboProject(':apphost-jobs')
    compile junboProject(':apphost-dataloader')
    compile junboProject(':apphost-emulators')
}

configurations.compile.dependencies.each { dep ->
    if (dep.name == "groovy-all") return
    tasks.installApp.dependsOn ":${dep.name}:startScripts"
    tasks.installApp.dependsOn ":${dep.name}:installApp"
    tasks.distZip.dependsOn ":${dep.name}:startScripts"
    tasks.distZip.dependsOn ":${dep.name}:installApp"
}

applicationDistribution.with {
    duplicatesStrategy 'exclude'

    configurations.compile.dependencies.each { dep ->
        if (dep.name == "groovy-all") return

        into("bin") {
            from("../${dep.name}/build/scripts")
        }
        into("lib") {
            from("../${dep.name}/build/install/${dep.name}/lib")
        }
    }

    into("dbsetup") {
        into("couchdb")     { from("../../couchdb") }
        into("liquibase")   { from("../../liquibase") }
        into("memcache")    { from("../../memcache") }
        into("pgha")        { from("../../scripts/pgha") }
    }

    from("${project.buildDir}/generated-res") { include("git.properties") }

    eachFile { FileCopyDetails f ->
        if (!f.name.endsWith('.sh')) return
        f.filter(FixCrLfFilter, eol: FixCrLfFilter.CrLf.newInstance("lf"))
    }
}

task('silkcloud', dependsOn: [ 'installApp', 'distZip' ])

defaultTasks 'silkcloud'
